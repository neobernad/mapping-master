options {
  LOOKAHEAD = 1;
  IGNORE_CASE = true;
  STATIC = false;
  USER_TOKEN_MANAGER=false;
  USER_CHAR_STREAM=false;
  OUTPUT_DIRECTORY = "../../../target/generated-sources/javacc";

  // JJTree options

  MULTI=true;
  NODE_DEFAULT_VOID = true;
  NODE_PACKAGE = "org.mm.parser.node";
  JJTREE_OUTPUT_DIRECTORY = "../../../target/generated-sources/jjtree";
}

PARSER_BEGIN(MappingMasterParser)

package org.mm.parser;

import static com.google.common.base.Preconditions.checkNotNull;
import javax.annotation.Nonnull;

import org.mm.renderer.internal.ReferenceNotation;

import org.mm.directive.ReferenceDirectives;
import org.mm.directive.DefaultDirectives;

import java.util.*;

public class MappingMasterParser {

  private ReferenceDirectives systemDirectives;
  private ReferenceDirectives localDirectives;
  private ReferenceDirectives userDirectives;

  public MappingMasterParser(@Nonnull java.io.InputStream inputStream, int unused) {
    this(inputStream, new DefaultDirectives(), unused);
  }

  public MappingMasterParser(@Nonnull java.io.InputStream inputStream, @Nonnull ReferenceDirectives systemDirectives, int unused) {
    this(checkNotNull(inputStream));
    this.systemDirectives = checkNotNull(systemDirectives);
    this.localDirectives = new ReferenceDirectives(systemDirectives);
  }

  public SimpleNode getTransformationRuleNode() throws ParseException {
    return transformation_rule();
  }
}

PARSER_END(MappingMasterParser)

/* White space */

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

/* Comments */

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
|  <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
|  "/*" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT
}

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}

/* Manchester Syntax keywords */

TOKEN: { <CLASS: "Class:" | "class:"> }
TOKEN: { <INDIVIDUAL: "Individual:" | "individual:"> }
TOKEN: { <EQUIVALENT_TO: "EquivalentTo:" | "equivalentTo:"> }
TOKEN: { <SUBCLASS_OF: "SubClassOf:" | "subClassOf:"> }
TOKEN: { <FACTS: "Facts:" | "facts:"> }
TOKEN: { <TYPES: "Types:" | "types:"> }
TOKEN: { <ANNOTATIONS: "Annotations:" | "annotations:"> }
TOKEN: { <SAME_AS: "SameAs:" | "sameAs:"> }
TOKEN: { <DIFFERENT_FROM: "DifferentFrom:" | "differentFrom:"> }
TOKEN: { <SOME: "some" | "SOME">}
TOKEN: { <ONLY: "only" | "ONLY">}
TOKEN: { <VALUE: "value" | "VALUE">}
TOKEN: { <MIN: "min" | "MIN">}
TOKEN: { <MAX: "max" | "MAX">}
TOKEN: { <EXACTLY: "exactly" | "EXACTLY">}
TOKEN: { <AND: "and" | "AND">}
TOKEN: { <OR: "or" | "OR">}
TOKEN: { <NOT: "not" | "NOT">}

/* Boolean keywords */

TOKEN: {<TRUE: "true" | "TRUE">}
TOKEN: {<FALSE: "false" | "FALSE">}

/* Datatypes */

TOKEN: { <XSD_STRING: "xsd:string" > }
TOKEN: { <XSD_DECIMAL: "xsd:decimal" > }
TOKEN: { <XSD_BYTE: "xsd:byte" > }
TOKEN: { <XSD_SHORT: "xsd:short" > }
TOKEN: { <XSD_INTEGER: "xsd:integer" > }
TOKEN: { <XSD_LONG: "xsd:long" > }
TOKEN: { <XSD_FLOAT: "xsd:float"  > }
TOKEN: { <XSD_DOUBLE: "xsd:double" > }
TOKEN: { <XSD_BOOLEAN: "xsd:boolean" > }
TOKEN: { <XSD_DATETIME: "xsd:dateTime"> }
TOKEN: { <XSD_TIME: "xsd:time"> }
TOKEN: { <XSD_DURATION: "xsd:duration" > }
TOKEN: { <XSD_DATE: "xsd:date" > }
TOKEN: { <RDF_XMLLITERAL: "rdf:XMLLiteral" > }
TOKEN: { <RDF_PLAINLITERAL: "rdf:PlainLiteral" > }

/*
TOKEN: { <XSD_NOTATION: "xsd:NOTATION" > }
TOKEN: { <XSD_UNSIGNEDLONG: "xsd:unsignedLong" > }
TOKEN: { <XSD_ANYSIMPLETYPE: "xsd:anySimpleType" > }
TOKEN: { <XSD_HEXBINARY: "xsd:hexBinary" > }
TOKEN: { <XSD_NONPOSITIVEINTEGER: "xsd:nonPositiveInteger" > }
TOKEN: { <XSD_NONNEGATIVEINTEGER: "xsd:nonNegativeInteger" > }
TOKEN: { <XSD_NORMALIZEDSTRING: "xsd:normalizedString" > }
TOKEN: { <XSD_POSITIVEINTEGER: "xsd:positiveInteger" > }
TOKEN: { <XSD_ENTITY: "xsd:ENTITY" > }
TOKEN: { <XSD_LANGUAGE: "xsd:language" > }
TOKEN: { <XSD_GMONTHDAY: "xsd:gMonthDay" > }
TOKEN: { <XSD_ANYURI: "xsd:anyURI" > }
TOKEN: { <XSD_NAME: "xsd:Name" > }
TOKEN: { <XSD_GYEARMONTH: "xsd:gYearMonth" > }
TOKEN: { <XSD_QNAME: "xsd:QName" > }
TOKEN: { <XSD_NEGATIVEINTEGER: "xsd:negativeInteger" > }
TOKEN: { <XSD_BASE64BINARY: "xsd:base64Binary" > }
TOKEN: { <XSD_ID: "xsd:ID" > }
TOKEN: { <XSD_TOKEN: "xsd:token" > }
TOKEN: { <XSD_UNSIGNEDINT: "xsd:unsignedInt" > }
TOKEN: { <XSD_IDREF: "xsd:IDREF" > }
TOKEN: { <XSD_UNSIGNEDBYTE: "xsd:unsignedByte" > }
TOKEN: { <XSD_GDAY: "xsd:gDay" > }
TOKEN: { <XSD_GYEAR: "xsd:gYear" > }
TOKEN: { <XSD_NCNAME: "xsd:NCName" > }
TOKEN: { <XSD_BYTE: "xsd:byte" > }
TOKEN: { <XSD_GMONTH: "xsd:gMonth" > }
TOKEN: { <XSD_UNSIGNEDSHORT: "xsd:unsignedShort" > }
TOKEN: { <XSD_NMTOKEN: "xsd:NMTOKEN" > }
*/

/* MappingMaster tokens */

TOKEN: { <RDF_ID: "rdf:ID"> }
TOKEN: { <XML_LANG: "xml:lang" > }

TOKEN: { <MM_DEFAULT_IRI_ENCODING: "mm:DefaultIRIEncoding"> }
TOKEN: { <MM_DEFAULT_SHIFT_DIRECTION: "mm:DefaultShiftDirection"> }
TOKEN: { <MM_DEFAULT_REFERENCE_TYPE: "mm:DefaultReferenceType"> }
TOKEN: { <MM_DEFAULT_PROPERTY_TYPE: "mm:DefaultPropertyType"> }
TOKEN: { <MM_DEFAULT_VALUE_DATATYPE: "mm:DefaultValueDatatype"> }

TOKEN: { <MM_DEFAULT_ORDER_IF_CELL_EMPTY: "mm:DefaultOrderIfCellEmpty"> }
TOKEN: { <MM_DEFAULT_ORDER_IF_ENTITY_ABSENT: "mm:DefaultOrderIfEntityAbsent"> }

TOKEN: { <MM_PREFIX: "mm:Prefix"> }
TOKEN: { <MM_NAMESPACE: "mm:Namespace"> }
TOKEN: { <MM_IRI_ENCODING: "mm:IRIEncoding"> }

TOKEN: { <MM_UNTYPED: "mm:Untyped"> }
TOKEN: { <MM_NULL: "mm:Null" > }

TOKEN: { <CELL_WILDCARD: "*"> }

TOKEN: { <OWL_CLASS: "Class"> }
TOKEN: { <OWL_NAMED_INDIVIDUAL: "Individual"> }
TOKEN: { <OWL_OBJECT_PROPERTY: "ObjectProperty"> }
TOKEN: { <OWL_DATA_PROPERTY: "DataProperty"> }
TOKEN: { <OWL_ANNOTATION_PROPERTY: "AnnotationProperty"> }
TOKEN: { <OWL_LITERAL: "Literal"> }
TOKEN: { <OWL_IRI: "IRI"> }

TOKEN: { <MM_TO_UPPER_CASE: "mm:toUpperCase"> }
TOKEN: { <MM_TO_LOWER_CASE: "mm:toLowerCase"> }
TOKEN: { <MM_TRIM: "mm:trim"> }
TOKEN: { <MM_PRINTF: "mm:printf"> }
TOKEN: { <MM_DECIMAL_FORMAT: "mm:decimalFormat"> }
TOKEN: { <MM_CAPTURING: "mm:capturing"> }
TOKEN: { <MM_REVERSE: "mm:reverse"> }
TOKEN: { <MM_REPLACE: "mm:replace"> }
TOKEN: { <MM_REPLACE_ALL: "mm:replaceAll"> }
TOKEN: { <MM_REPLACE_FIRST: "mm:replaceFirst"> }
TOKEN: { <MM_APPEND: "mm:append"> }
TOKEN: { <MM_PREPEND: "mm:prepend"> }

TOKEN: { <MM_NO_ENCODE: "mm:noEncode"> }
TOKEN: { <MM_CAMELCASE_ENCODE: "mm:camelCaseEncode"> }
TOKEN: { <MM_SNAKECASE_ENCODE: "mm:snakeCaseEncode"> }
TOKEN: { <MM_UUID_ENCODE: "mm:uuidEncode"> }
TOKEN: { <MM_HASH_ENCODE: "mm:hashEncode"> }

TOKEN: { <MM_DEFAULT_LITERAL: "mm:DefaultLiteral"> }
TOKEN: { <MM_DEFAULT_LABEL: "mm:DefaultLabel"> }

TOKEN: { <MM_DEFAULT_PREFIX: "mm:DefaultPrefix"> }
TOKEN: { <MM_DEFAULT_NAMESPACE: "mm:DefaultNamespace"> }
TOKEN: { <MM_DEFAULT_LANGUAGE: "mm:DefaultLanguage"> }

TOKEN: { <MM_NO_SHIFT: "mm:NoShift"> }
TOKEN: { <MM_SHIFT_UP: "mm:shiftUp"> }
TOKEN: { <MM_SHIFT_DOWN: "mm:shiftDown"> }
TOKEN: { <MM_SHIFT_LEFT: "mm:shiftLeft"> }
TOKEN: { <MM_SHIFT_RIGHT: "mm:shiftRight"> }

TOKEN: { <MM_CREATE_IF_CELL_EMPTY: "mm:createIfCellEmpty"> }
TOKEN: { <MM_IGNORE_IF_CELL_EMPTY: "mm:ignoreIfCellEmpty"> }
TOKEN: { <MM_WARNING_IF_CELL_EMPTY: "mm:warningIfCellEmpty"> }
TOKEN: { <MM_ERROR_IF_CELL_EMPTY: "mm:errorIfCellEmpty"> }

TOKEN: { <MM_CREATE_IF_ENTITY_ABSENT: "mm:createIfEntityAbsent"> }
TOKEN: { <MM_IGNORE_IF_ENTITY_ABSENT: "mm:ignoreIfEntityAbsent"> }
TOKEN: { <MM_WARNING_IF_ENTITY_ABSENT: "mm:warningIfEntityAbsent"> }
TOKEN: { <MM_ERROR_IF_ENTITY_ABSENT: "mm:errorIfEntityAbsent"> }

TOKEN: { <CREATE: "CREATE"> }
TOKEN: { <IGNORE: "IGNORE"> }
TOKEN: { <WARNING: "WARNING"> }
TOKEN: { <ERROR: "ERROR"> }

TOKEN:
{
<IRI_REF: "<" (~[ ">","<", "\"", "{", "}", "^", "\\", "|", "`", "\u0000"-"\u0020"])* ">" >
| <STRING_LITERAL: "\"" (~["\""])* ("\"\"" (~["\""])*)* ("\\\"")* "\"" >
| <INTEGER_LITERAL: (["-"])? (<DIGIT>)+ > 
| <LONG_LITERAL: (["-"])? (<DIGIT>)+> 
| <FLOAT_LITERAL: (["-"])? (<DIGIT>)+ "." (<DIGIT>)+ (<EXPONENT>)?
                  | (["-"])? "." (<DIGIT>)+ (<EXPONENT>)?
                  | (["-"])? (<DIGIT>)+ <EXPONENT>
                  | (["-"])? (<DIGIT>)+ (<EXPONENT>)? > 
| <QUOTED_NAME: "'" (~["'"])* ("''" (~["'"])*)* "'" >
| <#LETTER: [ "a"-"z", "A"-"Z" ]>
| <#DIGIT: ["0"-"9"] >
| <#EXPONENT: ["e", "E"] (["+", "-"])? (<DIGIT>)+ >
| <CELL_REFERENCE: ((<LETTER>)+ | "*") ((<DIGIT>)+ | "*")>
| <NAME: <LETTER> ( <LETTER> | <DIGIT> | "_" | "-" | "." )* >
}

//------------------------------------------------------------------------------------------
// MM Transformation Rule
//------------------------------------------------------------------------------------------

SimpleNode transformation_rule() #TransformationRule : {}
{
  ( rule_directive()
    | rule_expression()
  ) <EOF>
  {
    return jjtThis;
  }
}

//------------------------------------------------------------------------------------------
// MM Rule Directives
//------------------------------------------------------------------------------------------

void rule_directive() : {}
{
  ( mm_default_prefix_directive()
    | mm_default_namespace_directive()
    | mm_default_language_directive()
    | mm_default_reference_type_directive()
    | mm_default_property_type_directive()
    | mm_default_value_datatype_directive()
    | mm_default_iri_encoding_directive()
    | mm_default_label_value_directive()
    | mm_default_literal_value_directive()
    | mm_default_order_if_cell_empty_directive()
    | mm_default_order_if_entity_absent_directive()
  )
}

void mm_default_prefix_directive() :
{
  String defaultPrefix;
}
{
  <MM_DEFAULT_PREFIX> "=" defaultPrefix = string_value()
  {
    localDirectives = localDirectives.setPrefix(defaultPrefix);
  }
}

void mm_default_namespace_directive() :
{
  String defaultNamespace;
}
{
  <MM_DEFAULT_NAMESPACE> "=" defaultNamespace = string_value()
  {
    localDirectives = localDirectives.setNamespace(defaultNamespace);
  }
}

void mm_default_language_directive() :
{
  String defaultLanguage;
}
{
  <MM_DEFAULT_LANGUAGE> "=" defaultLanguage = string_value()
  {
    localDirectives = localDirectives.setLanguage(defaultLanguage);
  }
}

void mm_default_reference_type_directive() :
{
  int defaultEntityType;
}
{
  <MM_DEFAULT_REFERENCE_TYPE> "=" defaultEntityType = reference_type()
  {
    localDirectives = localDirectives.setEntityType(defaultEntityType);
  }
}

void mm_default_property_type_directive() :
{
  int defaultPropertyType;
}
{
  <MM_DEFAULT_PROPERTY_TYPE> "=" defaultPropertyType = property_type()
  {
    localDirectives = localDirectives.setPropertyType(defaultPropertyType);
  }
}

void mm_default_value_datatype_directive() :
{
  int defaultValueDatatype;
}
{
  <MM_DEFAULT_VALUE_DATATYPE> "=" defaultValueDatatype = datatype()
  {
    localDirectives = localDirectives.setValueDatatype(defaultValueDatatype);
  }
}

void mm_default_iri_encoding_directive() :
{
  int defaultIriEncoding;
}
{
  <MM_DEFAULT_IRI_ENCODING> "=" defaultIriEncoding = iri_encoding()
  {
    localDirectives = localDirectives.setIriEncoding(defaultIriEncoding);
  }
}

void mm_default_shift_direction_directive() :
{
  int defaultShiftDirection;
}
{
  <MM_DEFAULT_SHIFT_DIRECTION> "=" defaultShiftDirection = shift_direction()
  {
    localDirectives = localDirectives.setShiftDirection(defaultShiftDirection);
  }
}

void mm_default_label_value_directive() :
{
  String defaultLabelValue;
}
{
  <MM_DEFAULT_LABEL> "=" defaultLabelValue = string_value()
  {
    localDirectives = localDirectives.setLabelValue(defaultLabelValue);
  }
}

void mm_default_literal_value_directive() :
{
  String defaultLiteralValue;
}
{
  <MM_DEFAULT_LITERAL> "=" defaultLiteralValue = string_value()
  {
    localDirectives = localDirectives.setLiteralValue(defaultLiteralValue);
  }
}

void mm_default_order_if_cell_empty_directive() :
{
  int defaultOrderIfCellEmpty;
}
{
  <MM_DEFAULT_ORDER_IF_CELL_EMPTY> "=" defaultOrderIfCellEmpty = order_type()
  {
    localDirectives = localDirectives.setOrderIfCellEmpty(defaultOrderIfCellEmpty);
  }
}

void mm_default_order_if_entity_absent_directive() :
{
  int defaultOrderIfEntityAbsent;
}
{
  <MM_DEFAULT_ORDER_IF_ENTITY_ABSENT> "=" defaultOrderIfEntityAbsent = order_type()
  {
    localDirectives = localDirectives.setOrderIfEntityAbsent(defaultOrderIfEntityAbsent);
  }
}

//------------------------------------------------------------------------------------------
// MM Rule Expression
//------------------------------------------------------------------------------------------

void rule_expression() #RuleExpression : {}
{
  ( class_frame()
   | individual_frame()
  )
  // TODO datatype, object property, data property, and annotation property declarations
  // TODO negative property assertions, disjoint classes, disjoint properties, equivalent properties
  // See: http://www.w3.org/TR/owl2-manchester-syntax/
}

//------------------------------------------------------------------------------------------
// MM Class Frame
//------------------------------------------------------------------------------------------

void class_frame() #ClassFrame : {}
{
  class_declaration()
  ( subclass_of()
    | equivalent_classes()
    | annotation_assertion()
  )*
  // TODO Other clauses: DisjointWith, DisjoinUnionOf, HasKey
  // See: http://www.w3.org/TR/owl2-manchester-syntax/
}

void class_declaration() #ClassDeclaration : {}
{
  <CLASS> atomic_class()
}

void subclass_of() #SubclassOf : {}
{
  <SUBCLASS_OF> class_expression() ( "," class_expression() )*
}

void equivalent_classes() #EquivalentClasses : {}
{
  <EQUIVALENT_TO> class_expression() ( "," class_expression() )*
}

void annotation_assertion() #AnnotationAssertion : {}
{
  <ANNOTATIONS> annotation() ( "," annotation() )*
}

void annotation() #Annotation : {}
{
  annotation_property() annotation_value()
}

void annotation_value() #AnnotationValue : {}
{
  ( iri(OWL_IRI)
    | literal()
    | reference(MM_UNTYPED)
  )
}

//------------------------------------------------------------------------------------------
// MM Individual Frame
//------------------------------------------------------------------------------------------

void individual_frame() #IndividualFrame : {}
{
  individual_declaration()
  ( class_assertion()
    | property_assertion()
    | annotation_assertion()
    | same_as()
    | different_from()
  )*
}

void individual_declaration() #IndividualDeclaration : {}
{
  <INDIVIDUAL> named_individual()
}

void class_assertion() #ClassAssertion : {}
{
  <TYPES> class_expression() ( "," class_expression() )*
}

void property_assertion() #PropertyAssertion : {}
{
  <FACTS> fact() ( "," fact() )*
}

void fact() #Fact : {}
{
  ( property_fact()
    | referenced_fact()
  )
}

void property_fact() #PropertyFact : {}
{
  property() property_value()
}

void referenced_fact() #ReferencedFact : {}
{
  referenced_property() property_value()
}

void property_value() #PropertyValue : {}
{
  ( literal()
    | iri(MM_UNTYPED)
    | reference(MM_UNTYPED)
  )
}

void same_as() #SameAs : {}
{
  <SAME_AS> named_individual() ( "," named_individual() )*
}

void different_from() #DifferentFrom : {}
{
  <DIFFERENT_FROM>  named_individual() ( "," named_individual() )*
}

//------------------------------------------------------------------------------------------
// OWL Class Expression
//------------------------------------------------------------------------------------------

void class_expression() #ClassExpressionCategory : {}
{
  ( LOOKAHEAD(4) restriction()
    | atomic_class()
    | "(" boolean_class_expression() ")"
    | object_one_of()
  )
}

void atomic_class() #Class : {}
{
  value(OWL_CLASS) | reference(OWL_CLASS)
}

void boolean_class_expression() : {}
{
  ( object_union_of()
    | object_complement_of()
  )
}

void object_union_of() #ObjectUnion : {}
{
  object_intersection_of() ( <OR> object_intersection_of() )*
}

void object_intersection_of() #ObjectIntersection : {}
{
  class_expression() ( <AND> class_expression() )*
}

void object_complement_of() #ObjectComplement : {}
{
  <NOT> class_expression()
}

void object_one_of() #ObjectOneOf : {}
{
  "{" named_individual() ( "," named_individual() )* "}"
}

//------------------------------------------------------------------------------------------
// OWL Restriction
//------------------------------------------------------------------------------------------

void restriction() : {}
{
  ( LOOKAHEAD(12) exact_cardinality()
    | LOOKAHEAD(12) max_cardinality()
    | LOOKAHEAD(12) min_cardinality()
    | LOOKAHEAD(12) has_value()
    | LOOKAHEAD(12) all_values_from()
    | LOOKAHEAD(12) some_values_from()
  )
}

void exact_cardinality() : {}
{
  ( LOOKAHEAD(4) data_exact_cardinality()
    | LOOKAHEAD(4) object_exact_cardinality()
    | referenced_exact_cardinality()
  )
}

void data_exact_cardinality() #DataExactCardinality :
{
  int datatype;
}
{
  data_property() <EXACTLY> cardinality_value() datatype = datatype()
  {
    jjtThis.datatype = datatype;
  }
}

void object_exact_cardinality() #ObjectExactCardinality :
{
  boolean hasExplicitClassExpression = false;
}
{
  object_property() <EXACTLY> cardinality_value()
      ( class_expression() { hasExplicitClassExpression = true; } )?
  {
    jjtThis.hasExplicitClassExpression = hasExplicitClassExpression;
  }
}

void referenced_exact_cardinality() #ReferencedExactCardinality : {}
{
  referenced_property() <EXACTLY> cardinality_value() reference(MM_UNTYPED)
}

void max_cardinality() : {}
{
  ( LOOKAHEAD(4) data_max_cardinality()
    | LOOKAHEAD(4) object_max_cardinality()
    | referenced_max_cardinality()
  )
}

void data_max_cardinality() #DataMaxCardinality :
{
  int datatype;
}
{
  data_property() <MAX> cardinality_value() datatype = datatype()
  {
    jjtThis.datatype = datatype;
  }
}

void object_max_cardinality() #ObjectMaxCardinality :
{
  boolean hasExplicitClassExpression = false;
}
{
  object_property() <MAX> cardinality_value()
      ( class_expression() { hasExplicitClassExpression = true; } )?
      
  {
    jjtThis.hasExplicitClassExpression = hasExplicitClassExpression;
  }
}

void referenced_max_cardinality() #ReferencedMaxCardinality : {}
{
  referenced_property() <MAX> cardinality_value() reference(MM_UNTYPED)
}

void min_cardinality() : {}
{
  ( LOOKAHEAD(4) data_min_cardinality()
    | LOOKAHEAD(4) object_min_cardinality()
    | referenced_min_cardinality()
  )
}

void data_min_cardinality() #DataMinCardinality :
{
  int datatype;
}
{
  data_property() <MIN> cardinality_value() datatype = datatype()
  {
    jjtThis.datatype = datatype;
  }
}

void object_min_cardinality() #ObjectMinCardinality :
{
  boolean hasExplicitClassExpression = false;
}
{
  object_property() <MIN> cardinality_value()
      ( class_expression() { hasExplicitClassExpression = true; } )?
      
  {
    jjtThis.hasExplicitClassExpression = hasExplicitClassExpression;
  }
}

void referenced_min_cardinality() #ReferencedMinCardinality : {}
{
  referenced_property() <MIN> cardinality_value() reference(MM_UNTYPED)
}

void cardinality_value() #CardinalityValue : {}
{
  ( reference(OWL_LITERAL)
    | integer_literal()
  )
}

void has_value() : {}
{
  ( LOOKAHEAD(4) data_has_value()
    | LOOKAHEAD(4) object_has_value()
    | referenced_has_value()
  )
}

void data_has_value() #DataHasValue : {}
{
  data_property() <VALUE> literal_value()
}

void object_has_value() #ObjectHasValue : {}
{
  object_property() <VALUE> object_value()
}

void referenced_has_value() #ReferencedHasValue : {}
{
  referenced_property() <VALUE> reference(MM_UNTYPED)
}

void all_values_from() : {}
{
  ( LOOKAHEAD(3) data_all_values_from()
    | object_all_values_from()
  )
}

void data_all_values_from() #DataAllValuesFrom :
{
  int datatype;
}
{
  data_property() <ONLY> datatype = datatype()
  {
    jjtThis.datatype = datatype;
  }
}

void object_all_values_from() #ObjectAllValuesFrom : {}
{
  object_property() <ONLY> class_expression()
}

void some_values_from() : {}
{
  ( LOOKAHEAD(3) data_some_values_from()
    | object_some_values_from()
  )
}

void data_some_values_from() #DataSomeValuesFrom :
{
  int datatype;
}
{
  data_property() <SOME> datatype = datatype()
  {
    jjtThis.datatype = datatype;
  }
}

void object_some_values_from() #ObjectSomeValuesFrom : {}
{
  object_property() <SOME> class_expression()
}

//------------------------------------------------------------------------------------------
// OWL Property
//------------------------------------------------------------------------------------------

void referenced_property() #ReferencedProperty : {}
{
  reference(localDirectives.getPropertyType())
}

void property() #Property : {}
{
  value(MM_UNTYPED)
}

void data_property() #DataProperty : {}
{
  value(OWL_DATA_PROPERTY)
}

void object_property() #ObjectProperty : {}
{
  value(OWL_OBJECT_PROPERTY)
}

void annotation_property() #AnnotationProperty: {}
{
  ( value(OWL_ANNOTATION_PROPERTY)
    | reference(OWL_ANNOTATION_PROPERTY)
  )
}

//------------------------------------------------------------------------------------------
// OWL Named Individual
//------------------------------------------------------------------------------------------

void named_individual() #NamedIndividual : {}
{
  ( value(OWL_NAMED_INDIVIDUAL)
    | reference(OWL_NAMED_INDIVIDUAL)
  )
}

//------------------------------------------------------------------------------------------
// Value Category
//------------------------------------------------------------------------------------------

void value(int type) #Value : {}
{
  ( name(type)
    | iri(type)
    | literal()
  )
}

void literal_value() #LiteralValue : {}
{
  ( reference(OWL_LITERAL)
    | literal()
  )
}

void object_value() #ObjectValue() : {}
{
  ( reference(OWL_NAMED_INDIVIDUAL)
    | name(OWL_NAMED_INDIVIDUAL)
    | iri(OWL_NAMED_INDIVIDUAL)
  )
}

void reference(int type) #Reference :
{
  userDirectives = new ReferenceDirectives(localDirectives).setEntityType(type);
}
{
  reference_notation()
    (  LOOKAHEAD(2)
       "(" ( reference_directive() )* ( builtin_function() { jjtThis.hasBuiltInFunction = true; })? ")"
    )?
  {
    jjtThis.referenceDirectives = new ReferenceDirectives(userDirectives);
  }
}

void name(int type) #Name :
{
  String name;
}
{
  ( LOOKAHEAD(2) name = prefixed_name()
    | LOOKAHEAD(2) name = unprefixed_name()
  )
  {
    jjtThis.name = name;
    jjtThis.entityType = type;
  }
}

String prefixed_name() :
{
  Token t1;
  Token t2;
}
{
  t1 = <NAME> ":" t2 = <NAME>
  {
    return t1.image + ":" + t2.image;
  }
}

String unprefixed_name() :
{
  Token t;
}
{
  t = <NAME>
  {
    return t.image;
  }
}

void iri(int type) #Iri :
{
  Token t;
}
{
  t = <IRI_REF>
  {
    jjtThis.iri = t.image;
    jjtThis.entityType = type;
  }
}

void literal() #Literal : {}
{
  ( integer_literal()
   | float_literal()
   | string_literal()
   | boolean_literal()
  )
} 

void integer_literal() #IntegerLiteral :
{
  int value;
}
{
  value = integer_value()
  {
    jjtThis.lexicalValue = value;
  }
}

void float_literal() #FloatLiteral :
{
  float value;
}
{
  value = float_value()
  {
    jjtThis.lexicalValue = value;
  }
}

void string_literal() #StringLiteral :
{
  String value;
}
{
  value = string_value()
  {
    jjtThis.lexicalValue = value;
  }
}

void boolean_literal() #BooleanLiteral :
{
  boolean value;
}
{
  value = boolean_value()
  {
    jjtThis.lexicalValue = value;
  }
}

//------------------------------------------------------------------------------------------
// Cell Reference
//------------------------------------------------------------------------------------------

void reference_notation() #ReferenceNotation :
{
  String cellReference;
}
{
  ( "@" cellReference = cell_reference() )
  {
    jjtThis.referenceNotation = new ReferenceNotation(cellReference);
  }
}

String quoted_name() :
{
  Token t;
}
{
  t = <QUOTED_NAME>
  {
    String s = t.image;
    return s.substring(1, s.length() - 1);
  }
}

String cell_reference() :
{
  Token t;
}
{
  t = <CELL_REFERENCE>
  {
    return t.image;
  }
}

//------------------------------------------------------------------------------------------
// Reference Directives
//------------------------------------------------------------------------------------------

void reference_directive() :
{
  int value;
}
{
  ( prefix_directive()
   | namespace_directive()
   | language_directive()
   | value = reference_type() { userDirectives = userDirectives.setEntityType(value); }
   | value = datatype() { userDirectives = userDirectives.setValueDatatype(value).setEntityType(OWL_LITERAL); }
   | value = iri_encoding() { userDirectives = userDirectives.setIriEncoding(value); }
   | value = shift_direction() { userDirectives = userDirectives.setShiftDirection(value); }
   | value = order_if_cell_empty() { userDirectives = userDirectives.setOrderIfCellEmpty(value); }
   | value = order_if_entity_absent() { userDirectives = userDirectives.setOrderIfEntityAbsent(value); }
  )
}

void prefix_directive() :
{
  String prefixValue;
}
{
  <MM_PREFIX> "=" prefixValue = string_value()
  {
    userDirectives = userDirectives.setPrefix(prefixValue);
  }
}

void namespace_directive() :
{
  String namespaceValue;
}
{
  <MM_NAMESPACE> "=" namespaceValue = string_value()
  {
    userDirectives = userDirectives.setNamespace(namespaceValue);
  }
}

void language_directive() :
{
  String languageValue;
}
{
  <XML_LANG> ( "=" ( <MM_NULL> { languageValue = ""; }
                     | "*" { languageValue = "*"; }
                     | languageValue = string_value()
                   )
               | ( "!" "=" <MM_NULL> { languageValue = "+"; } )
             )
  {
    userDirectives = userDirectives.setLanguage(languageValue)
                            .setEntityType(OWL_LITERAL)
                            .setValueDatatype(RDF_PLAINLITERAL);
  }
}

//------------------------------------------------------------------------------------------
// Value Extraction Functions
//------------------------------------------------------------------------------------------

void builtin_function() #BuiltInFunction :
{
  Token t;
  int functionType;
}
{
  (
    ( t = <MM_TO_UPPER_CASE> { functionType = t.kind; }
      | t = <MM_TO_LOWER_CASE> { functionType = t.kind; }
      | t = <MM_TRIM> { functionType = t.kind; }
      | t = <MM_PRINTF> { functionType = t.kind; }
      | t = <MM_DECIMAL_FORMAT> { functionType = t.kind; }
      | t = <MM_REVERSE> { functionType = t.kind; }
      | t = <MM_CAPTURING> { functionType = t.kind; }
      | t = <MM_REPLACE> { functionType = t.kind; }
      | t = <MM_REPLACE_ALL> { functionType = t.kind; }
      | t = <MM_REPLACE_FIRST> { functionType = t.kind; }
      | t = <MM_APPEND> { functionType = t.kind; }
      | t = <MM_PREPEND> { functionType = t.kind; }
    ) ( "(" argument_list() ")" )?
    | ( "[" ( argument_list() )? "]" ) { functionType = MM_CAPTURING; }
  )
  {
    jjtThis.functionType = functionType;
  }
}

void argument_list() #ArgumentList : {}
{
  argument() ( "," argument() )*
}

void argument() #Argument : {}
{
  ( reference_notation()
    | literal()
  )
}

//------------------------------------------------------------------------------------------
// Literal Values
//------------------------------------------------------------------------------------------

int integer_value() :
{
  Token t;
}
{
  t = <INTEGER_LITERAL>
  {
    return Integer.parseInt(t.image);
  }
}

float float_value() :
{
  Token t;
}
{
  t = <FLOAT_LITERAL>
  {
    return Float.valueOf(t.image);
  }
}

String string_value() :
{
  Token t;
  String str;
}
{
  t = <STRING_LITERAL> 
  {
    str = t.image;
    return str.substring(1, str.length() - 1);
  }
}

boolean boolean_value() :
{
  Token t;
  boolean value;
}
{
  ( <TRUE> { value = true; } | <FALSE> { value = false; } )
  {
    return value;
  }
}

//------------------------------------------------------------------------------------------
// Literal Datatypes
//------------------------------------------------------------------------------------------

int datatype() :
{
  Token t;
}
{
  ( t = <XSD_STRING>
    | t = <XSD_DECIMAL>
    | t = <XSD_BYTE>
    | t = <XSD_SHORT> 
    | t = <XSD_INTEGER>
    | t = <XSD_LONG>
    | t = <XSD_FLOAT>
    | t = <XSD_DOUBLE>
    | t = <XSD_BOOLEAN>
    | t = <XSD_TIME>
    | t = <XSD_DATETIME>
    | t = <XSD_DATE>
    | t = <XSD_DURATION>
    | t = <RDF_PLAINLITERAL>
  )
  {
    return t.kind;
  }
}

//------------------------------------------------------------------------------------------
// Enumeration Types
//------------------------------------------------------------------------------------------

int iri_encoding() :
{
  Token t;
  int encodingType;
}
{
  ( t = <MM_NO_ENCODE> { encodingType = t.kind; }
    | t = <MM_CAMELCASE_ENCODE> { encodingType = t.kind; }
    | t = <MM_SNAKECASE_ENCODE> { encodingType = t.kind; }
    | t = <MM_UUID_ENCODE> { encodingType = t.kind; }
    | t = <MM_HASH_ENCODE> { encodingType = t.kind; }
  )
  {
    return encodingType;
  }
}

int reference_type() :
{
  Token t;
  int entityType;
}
{
  ( t = <OWL_CLASS> { entityType = t.kind; }
    | t = <OWL_NAMED_INDIVIDUAL> { entityType = t.kind; }
    | t = <OWL_OBJECT_PROPERTY> { entityType = t.kind; }
    | t = <OWL_DATA_PROPERTY> { entityType = t.kind; }
    | t = <OWL_ANNOTATION_PROPERTY> { entityType = t.kind; }
    | t = <OWL_IRI> { entityType = t.kind; }
    | t = <OWL_LITERAL> { entityType = t.kind; }
  )
  {
    return entityType;
  }
}

int property_type() :
{
  Token t;
  int propertyType;
}
{
  ( t = <OWL_DATA_PROPERTY> { propertyType = t.kind; }
    | t = <OWL_OBJECT_PROPERTY> { propertyType = t.kind; }
    | t = <OWL_ANNOTATION_PROPERTY> { propertyType = t.kind; }
  )
  {
    return propertyType;
  }
}

int shift_direction() :
{
  Token t;
  int shiftDirection;
}
{
  ( t = <MM_SHIFT_UP> { shiftDirection = t.kind; }
    | t = <MM_SHIFT_DOWN> { shiftDirection = t.kind; }
    | t = <MM_SHIFT_LEFT> { shiftDirection = t.kind; }
    | t = <MM_SHIFT_RIGHT> { shiftDirection = t.kind; }
    | t = <MM_NO_SHIFT> { shiftDirection = t.kind; }
  )
  {
    return shiftDirection;
  }
}

int order_if_cell_empty() :
{
  Token t;
  int orderIfCellEmpty;
}
{
  ( t = <MM_CREATE_IF_CELL_EMPTY> { orderIfCellEmpty = t.kind; }
    | t = <MM_IGNORE_IF_CELL_EMPTY> { orderIfCellEmpty = t.kind; }
    | t = <MM_WARNING_IF_CELL_EMPTY> { orderIfCellEmpty = t.kind; }
    | t = <MM_ERROR_IF_CELL_EMPTY> { orderIfCellEmpty = t.kind; }
  )
  {
    return orderIfCellEmpty;
  }
}

int order_if_entity_absent() :
{
  Token t;
  int orderIfEntityAbsent;
}
{
  ( t = <MM_CREATE_IF_ENTITY_ABSENT> { orderIfEntityAbsent = t.kind; }
    | t = <MM_IGNORE_IF_ENTITY_ABSENT> { orderIfEntityAbsent = t.kind; }
    | t = <MM_WARNING_IF_ENTITY_ABSENT> { orderIfEntityAbsent = t.kind; }
    | t = <MM_ERROR_IF_ENTITY_ABSENT> { orderIfEntityAbsent = t.kind; }
  )
  {
    return orderIfEntityAbsent;
  }
}

int order_type() :
{
  Token t;
  int orderType;
}
{
  ( t = <CREATE> { orderType = t.kind; }
    | t = <IGNORE> { orderType = t.kind; }
    | t = <WARNING> { orderType = t.kind; }
    | t = <ERROR> { orderType = t.kind; }
  )
  {
    return orderType;
  }
}